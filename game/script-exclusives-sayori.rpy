label sayori_exclusive_1:
    scene bg club_day
    with wipeleft_scene

    "Man. Seems like no one wants to be bothered today."
    "I slump down into the nearest desk."
    "How am I supposed to occupy myself with something literature-related when I'm alone like this?"
    "I guess I could start reading the book Yuuri gave me, but I'm so bored that reading would only put me to sleep."
    "I close my eyes and end up listening in on Satori and Mateo's conversation."
    show satori 1g at t21
    show mateo 6d at t22
    s "We're probably gonna sound really lame compared to all the other clubs though."
    m "Hmm...well, we can't give up."
    m 1r "The festival is our chance to show everyone what literature is all about."
    m 1d "The problem is, the idea of a literature club sounds too dense and intellectual."
    m 3g "But it's not like that at all."
    m 3i "We just need a way of showing that to everyone."
    m 1r "Something that speaks to their creative minds."
    s 2h "That doesn't solve our problem though."
    m 6i "What problem?"
    s 3j "Even if we come up with the most fun idea ever, who's gonna come to a literature event in the first place?"
    s 3c "It's more important for us to figure out how to get people to actually show up."
    s 3b "We need to first accumulate an audience, then we can speak to their creative minds."
    show satori 1b
    "Wow, Satori really is taking this seriously."
    "It's rare to hear him deliberating like this."
    m 6q "Hmm...very good point."
    m 4x "In that case, do you think food will do the trick?"
    s 7x "The right food will!"
    m 1ag "Are you thinking what I'm thinking?"
    s 5 "Donuts!"
    m 1k "That's right! Natsuko would love to handle that."
    s 4r "Yeah! That works out perfectly!"
    m 3b "Now to work out the details of the event itself..."
    "I find myself smiling...thinking about those donuts."
    scene black with close_eyes
    "Also, I admire Satori's dedication to this club."
    "I have so much trouble finding any motivation at all."
    "Satori, on the other hand, can put his mind to anything and make it happen."
    "That's why I let him get on my case about things."
    "I wonder what it'd be like to see the world through his eyes?..."
    scene bg club_day with open_eyes
    mc "Gah---!"
    show satori 4aa at face
    "I open my eyes to see Satori's face filling my vision."
    "I nearly fall out of my chair."
    show satori at h11
    s 1aa "Aw...did I wake baby from her wittle nap?"
    mc "You scared me half to death, ya big doofus!"
    s 1j "Well, tough!"
    s 7ac "You shouldn't have been sleeping in the first place!"
    s 1i "Unfortunately for you, there's no such thing as the napping club!"
    "I grab Satori's arm and look into his face with excited eyes."
    mc "Dude...we need to start the Napping Club."
    s 1j "You stayed up late again, huh?"
    s 7ac "You know, now that you're in this club, you're gonna have less time for video games. So get used to it."
    mc "Fine, fine. You're always looking out for me, Satori."
    s 1k "Well...someone has to."
    mc "And therein lies the problem, my friend."
    mc "You look out for me better than you look out for yourself."
    mc "You talk about me staying up late when you started oversleeping last year and continue to do so."
    mc "The only reason you wake up on time now is so you can make sure I wake up on time."
    mc "You don't wake up for you. You do it for me."
    mc "Do you ever do anything for yourself?"
    s 10y "Um..."
    mc "In fact...what the hell are you even wearing?"
    s 1n "Eh?"
    "Satori scans himself curiously."
    s 1o "Oh...my new uniform?"
    s 1l "Yeah...I accidentally got the wrong size."
    s 1g "Is it...is it that noticeable?"
    mc "You look like a 6 year old trying to wear his father's business suit."
    show satori 1ab
    mc "See, this is exactly what I'm talking about."
    mc "If you cared about yourself, you would have paid more attention to what you were ordering."
    mc "You're not even wearing it properly."
    mc "Look at the other guys."
    show satori 1b
    mc "See how their blazers are nicely buttoned and their ties tucked in?"
    mc "Why is yours still unbuttoned?"
    mc "You specifically got a new one because the other one was too tight when buttoned up."
    mc "Jeez, come here."

    scene s_cg1_base
    show s_cg1_exp1
    with dissolve_cg
    "I stand up and start fixing Satori's tie."
    "I tighten it properly and button up the collar of his white undershirt."
    "I tuck the tail into his sweater vest."
    "Then I get to work buttoning his blazer from the bottom, working my way up."
    "Satori smirks a little."
    s "You...you really shouldn't do these kinds of things in front of people."
    s "They might get the wrong idea about us."
    "I look up at Satori curiously."
    "He's blushing a little."
    "I'm tempted to make this situation as weird as possible for him."
    "It amuses me so."
    mc "Since when do we care what people think about us?"
    mc "You scared you won't get a girlfriend if everyone thinks we're a couple?"
    "Satori's blush intensifies a bit."
    show s_cg1_exp2 at cgfade
    s "Well, no..."
    s "It's...it's the guys..."
    s "If they think we're a couple, then you won't be able to..."
    s "You know..."
    s "Get one of them to ask you out."
    mc "You know...I really appreciate your help..."
    mc "But... Why do you care so much about hooking me up with one of your friends?"
    s "Huh?"
    s "Ah---! N-n-no reason!"
    s "I was just... Uh..."
    hide s_cg1_exp2
    show s_cg1_exp4 at cgfade
    s "Jeez, sorry I said anything!"
    "He averts his eyes."
    "Hehe... he's adorable when he's flustered."
    "I finish buttoning up his blazer."
    mc "There. That's better."
    mc "You know you're dressed right when your tie, buttons and belt buckle line up perfectly in a straight line."
    "I look down and notice his pants are crooked, the belt buckle off to one side."
    mc "Ah, hold on..."
    s "H-hey--! [player], wait!"
    "I reach down, my fingers curling into the front of his pants and gripping the waistband."
    "With one assertive but gentle tug, I line the belt buckle with the buttons on Satori's blazer."
    mc "There we go."
    hide s_cg1_exp4
    show s_cg1_exp3 at cgfade
    s "Jeez... w-what'd you do that for?"
    s "I could've handled it myself!"
    "I look up at him, smiling playfully."
    mc "Well... Maybe I wanted to handle it today."
    hide s_cg1_exp3
    show s_cg1_exp5 at cgfade
    "Satori's face turns bright red."
    "He retreats a few steps."
    s "Gah!"
    s "W-w-why do you always say weird things like that?! Sheesh!"
    "I snicker triumphantly."
    "I just can't help myself sometimes."
    mc "Anyway, you look much better now."
    hide s_cg1_exp5
    show s_cg1_exp4 at cgfade
    s "Ack--...I can't stand the collar buttoned up like that."
    s "I can hardly breath."
    s "Feels like I'm being strangled."
    s "And I can barely move in this stiff blazer when it's all buttoned up."
    s "Ugh...it's not worth it at all."
    "Satori hastily unbuttons his blazer once more, along with his shirt collar."
    "He also loosens his tie and frees it from beneath his sweater vest."

    scene bg club_day
    with dissolve_cg
    show satori 5z at t11
    s "Ah...much better!"
    "Having disheveled himself once again, Satori stretches in relief."
    s 1j "You have some nerve to talk about {i}my{/i} wardrobe."
    s 7j "What about you? Where even {i}is{/i} your blazer?"
    mc "What do you mean? It's right he-..."
    "I check my waist, where I usually keep my blazer tied, only to find that it's gone."
    "I feel my face burn with embarrassment."
    mc "Oh, damn it!"
    s 1ac "You lost it, didn't you?"
    s 7ad "Again!"
    mc "I...didn't lose it, alright?"
    mc "I just..."
    mc "...Don't know where it is right now..."
    s 1o "..."
    s 5p "They mean the same thing, dingus!!"
    mc "Oh, shut up."
    mc "It's probably just in the lost and found again."
    s 7ac "Or hanging on the back of the chair in the detention room."
    s 3ac "Or lying in the hallway."
    s 7ac "Or in the cafeteria..."
    mc "Whatever!"
    mc "It always ends up in the lost and found anyway!"
    s 3ab "It wouldn't end up there if you would just wear it like a normal person!"
    mc "C'mon..."
    "I look around nervously before lowering my voice a bit."
    mc "You know why I keep it tied around my waist..."
    show satori 1b
    "I tug embarrassingly at my skirt."
    "I use the blazer as...well...extra coverage."
    "Satori grins devilishly and gestures towards the others."
    s 1aa "You really want to get their attention?"
    s 4aa "Leave the blazer in the lost and found."
    mc "Hey, I don't need to show off the goods in order to get their attention!"
    mc "I happen to be pretty friggin' charming!"
    s 10s "Okay, okay!"
    s 10l "It was just a joke..."
    mc "Well, anyway...try to focus a bit more on taking care of yourself, alright?"
    mc "If you wake up early, do it for your benefit."
    s 4i "Only if you agree to start going to bed at a reasonable hour."
    mc "Deal."
    s 10y "I guess we really are better at taking care of each other than ourselves, huh?"
    mc "Yeah. I think it's always been that way."
    show satori at t21 zorder 3
    show mateo 6k at t22 zorder 2
    m "Alright, guys!"
    "Mateo suddenly calls out."
    m 6b "Why don't we share our poems now?"
    s 5z "Sweet! I can't wait to read yours, [player]!"
    mc "Yeah...likewise..."
    "I fail to sound enthusiastic as Satori jogs to his desk to retrieve his poem."
    show satori at thide
    return

label sayori_exclusive_2:
    if not renpy.music.get_playing(channel='music') == audio.t2:
        stop music fadeout 1.0
    scene bg club_day
    with wipeleft_scene
    if not renpy.music.get_playing(channel='music') == audio.t2:
        play music t2
    s "[player], [player]!"
    "Sayori suddenly comes up to me."
    show sayori 1x at t11 zorder 2
    s "I'm gonna go get some supplies from another classroom."
    s "Want to come with me?"
    mc "Supplies?"
    mc "What for?"
    s 2a "Well, you know how the festival is coming up?"
    s "Me and Monika were gonna make some posters and stuff."
    s "So I need to go find some crayons, and markers, and glue sticks..."
    mc "Ah, I see."
    mc "Sure, I'll go with you."
    s 4q "Yaay~"
    s 4x "Okay Monika, we'll be back soon!"
    show sayori at t22 zorder 2
    show monika 1a at f21 zorder 3
    m "Ah, are you going with [player] to get the supplies?"
    m "There's no need to trouble yourself."
    m "I'd be happy to go with him."
    show monika at t21 zorder 2
    show sayori 1h at s22
    s "Aw, but I wanted to go!"
    s "It's so much fun exploring empty classrooms and stuff!"
    show monika at f21 zorder 3
    m 2j "Hehe, okay, okay."
    m 2a "It was just a suggestion."
    m "See if you can find poster paper too, okay?"
    show monika at t21 zorder 2
    show sayori at f22 zorder 3
    s 1r "Okaay~!"
    s 1a "Ready, [player]?"
    mc "Yep, let's go."
    scene bg corridor
    show sayori 4a at t11 zorder 2
    with wipeleft_scene
    "Sayori and I exit the clubroom."
    "I follow behind as Sayori hums and skips around the hallway."
    "Honestly..."
    "It feels like I'm taking a kid to the mall or something."
    "Sayori finds pleasure in the simplest things sometimes."
    mc "Hey, Sayori..."
    mc "What exactly are we doing for the festival, anyway?"
    mc "I'm not sure how you would make an event out of literature..."
    s 1q "Ehehe!"
    s 1x "Me and Monika have it all planned out!"
    s "Don't you worry~"
    mc "Is that so...?"
    s "Yup!"
    s "We're gonna do a poetry performance!"
    mc "A performance...?"
    mc "Of what kind?"
    s 1c "Well..."
    s "Everyone is gonna take turns on stage..."
    s 1x "And recite their favorite poems!"
    mc "Ah..."
    mc "That sounds..."
    "...Kind of dull?"
    s 1h "[player]!"
    s "You're not thinking about it the right way at all!"
    s "It's not just about reading poems..."
    s 1x "It's about performing them!"
    s "Like, you say the lines of the poem like..."
    s 2j "{i}Between my feet...{/i}"
    s "{i}The last remaining flower beckons to me.{/i}"
    s 1c "{i}I twist the stem, freeing it from its clinging roots...{/i}"
    s "{i}Caressing the final joyous moment between my fingers.{/i}"
    s 3g "{i}But to what ends have I summoned this joy?{/i}"
    s "{i}For now when I look in every direction...{/i}"
    s 1j "{i}The once-prosperous field before me...{/i}"
    show sayori at h11
    s 4m "{i}Is but a barren wasteland!{/i}"
    s "..."
    s 1r "Like that!!"
    mc "Sayori..."
    "How do I put this..."
    mc "I'm sure it's just me, but it's impossible for me to take you seriously when you talk like that..."
    show sayori 5c at s11
    s "Ehhh?"
    s "You meanie!"
    s "I'm working super hard on this, you know!"
    mc "Ah, I know, I know!"
    mc "I just meant that it's a pretty unordinary contrast to your cute self."
    show sayori 4s at t11 zorder 2
    s "Ahaha! Don't say that, it's embarrassing!"
    s 4y "But I guess that means I'm doing a good job~"
    mc "Yeah, I guess so..."
    show sayori at h11
    s 4r "Aaah, I'm so excited!"
    s "The festival is going to be so much fun~"
    "Sayori spins herself around in the hallway again."
    s 1x "Hey, [player], this classroom over here is empty!"
    s "Let's begin the mission!"
    show sayori at thide zorder 1
    hide sayori
    mc "The mission, eh...?"
    "It's been a long time since I've spent time with Sayori like this."
    "But in the end, she hasn't changed one bit."
    "She's nothing but a ball of sunshine, drawing happy vibes from the world around her."
    "It's a pretty nostalgic feeling for me."
    "As the years went by, I began to hole myself up in my room more and more."
    "So going adventuring with Sayori brings about a special sort of feeling I forgot I had in me."
    scene bg class_day
    with wipeleft_scene
    "The two of us enter the classroom."
    "Sayori heads straight to the closet, and I follow."
    show sayori 1b at t11 zorder 2
    s "Let's see what we have in here..."
    s 4x "...Crayons!!"
    "Sayori pulls a box full of crayons off the shelf."
    s "They're the best brand, too!"
    s 1b "They're kind of dirty, though..."
    "Sayori starts pulling various crayons out of the box, reading the color names."
    mc "Alright, that's one down."
    mc "Don't get distracted, we still need to find--"
    s 1a "Wait, I'm looking for my favorite color..."
    mc "Fine, fine..."
    mc "Then at least move aside so I can look for the poster paper."
    s 1b "Ah, I dropped one by accident--"
    play sound "sfx/smack.ogg"
    show white zorder 4:
        alpha 0.6
        linear 0.25 alpha 0.0
    show sayori 2p at h11
    "{i}Smack!{/i}"
    hide white
    s "Kya--!"
    "Sayori bends over and smacks her forehead right into the shelf."
    "She falls to the floor, and the crayons spill all over her lap."
    show sayori 4p at s11
    s "Owowowowow..."
    mc "You okay?"
    s "My forehead..."
    "Sayori clutches her forehead."
    mc "Jeez, Sayori..."
    mc "That's just like you, isn't it?"
    mc "C'mon, let me see."
    "Since Sayori is sitting on the floor, I grab her by the waist and pull her out of the closet."
    mc "You have to move your hands, Sayori..."
    s 4g "But it hurts..."
    mc "Just do it for a second."
    $ persistent.clear[7] = True
    scene s_cg2_base1
    show s_cg2_exp2
    with dissolve_cg
    "Sayori slowly releases her hands from her forehead."
    "I gently brush her bangs to the side."
    show s_cg2_exp1 at cgfade
    show s_cg2_exp3 at cgfade
    s "Ow--!"
    mc "Sorry..."
    "There's a huge red mark on the center of her forehead."
    "A bump is starting to form, as well."
    mc "Man, that's gonna swell up."
    mc "I should find you some ice..."
    hide s_cg2_exp3
    hide s_cg2_exp1
    s "[player]..."
    mc "Where would I even find ice around this time...?"
    mc "Ah, I guess a cold drink would do..."
    s "You don't have to--!"
    show s_cg2_exp2 at cgfade
    hide s_cg2_exp2
    s "I'm fine with--looking like a unicorn--"
    "Even wincing from the pain, Sayori makes a silly joke."
    mc "Ahaha, what are you saying?"
    mc "I'll be right back, okay?"
    s "O-Okay..."
    stop music fadeout 1.0
    scene bg corridor with wipeleft_scene
    "I pat Sayori on the shoulder and run out into the hallway."
    "I locate the nearest vending machine."
    mc "What should I get...?"
    "It doesn't really matter, since it will be used as an ice pack, rather than to drink."
    "But I know Sayori likes apple juice, so I purchase that one."
    "In just a moment, I'm already returning to the classroom where I left Sayori."
    scene s_cg2_base1
    show s_cg2_exp2
    with wipeleft_scene
    play music t9
    "She has one palm on her forehead and is using the other hand to clumsily scoop crayons back into the box."
    s "At least they were already in the wrong spots before I spilled them..."
    mc "Sayori, here."
    show s_cg2_base2 at cgfade behind s_cg2_exp2
    "I hand Sayori the bottle of apple juice."
    show s_cg2_exp2 at cgfade
    hide s_cg2_exp2
    s "It's nice and cold..."
    "Sayori opens the cap and starts drinking from it."
    mc "Sayori, what are you doing?!"
    mc "It's for your forehead, idiot!"
    show s_cg2_exp3 at cgfade
    s "Ah--"
    s "Sorry, I forgot~"
    s "Ahahaha!"
    mc "How hard did you hit your head...?"
    "Sayori places the bottle against the bump on her head."
    show s_cg2_exp1 at cgfade
    s "It stings..."
    mc "Just bear with it, it'll feel better soon."
    mc "Looks like you cleaned up most of the crayons, so that's good."
    hide s_cg2_exp1
    hide s_cg2_exp3
    s "Hey, [player]..."
    s "This kind of reminds you of growing up, doesn't it...?"
    mc "Eh? What do you mean?"
    s "You know how we used to play outside all the time..."
    s "I would always try to keep up with you."
    s "You were kind of oblivious in some ways..."
    s "Like I usually fell behind or had trouble climbing on the things you did..."
    s "But sometimes when I tried to do things I couldn't, I would get myself hurt."
    s "I'd fall and scrape myself, or get a bump..."
    s "And I would start crying really hard."
    show s_cg2_exp3 at cgfade
    s "Ahaha!"
    s "And you would rush over as quick as you could."
    hide s_cg2_exp3
    s "You would try really hard to get me to stop crying."
    s "It was almost like you blamed yourself and were afraid of getting in trouble if someone found out..."
    s "Even though it really wasn't your fault at all, you know?"
    mc "Did I really do that...?"
    s "Yeah...you don't remember?"
    mc "Come to think of it, maybe I do remember a bit..."
    mc "I guess I was always so focused on my games that I didn't pay enough attention to you."
    mc "So in a way, it was my fault."
    mc "Kind of like this time, too..."
    mc "If I wasn't rushing you out of the closet, you probably wouldn't have hit your head."
    s "[player]..."
    s "I don't think you realize it, but you're always thinking about other people."
    s "Even after all these years..."
    s "You're rushing to help me, even though I'm just being clumsy."
    show s_cg2_exp3 at cgfade
    s "You're really a sweetheart..."
    mc "D-Don't call me that!"
    mc "And I don't really do this kind of thing all the time..."
    mc "I guess when it comes to you, it just feels natural."
    mc "Before I even know it, I'm treating you like that."
    mc "I guess that's what happens when you've been friends for so long."
    hide s_cg2_exp3
    s "Really...?"
    s "Maybe you're right..."
    s "[player]..."
    s "I'm so glad that nothing's changed between us."
    s "Do you think it'll be like this forever?"
    mc "Forever...?"
    "If I'm honest to myself..."
    "There's no telling where we'll each end up for college, or after that."
    "So it wouldn't be fair for me to make any promises."
    "But..."
    mc "Well, I hope so."
    mc "It's been this long already, right?"
    mc "I can't imagine you ever changing, so my hopes are up."
    s "I'm so happy..."
    "Sayori has a whimsical expression in her eyes."
    "We remain silent for a moment."
    "She's so silly and clumsy on the outside that when I see her deep in thought like this..."
    "It makes me not want to disturb her."
    s "I guess we should go back..."
    s "I don't want to worry Monika, you know?"
    mc "Good luck with that."
    mc "She's gonna see your forehead either way."
    s "Not if I hide it under my bangs~"
    play music t8 fadeout 1.0
    scene bg class_day
    show sayori 1a at i11 zorder 2
    with dissolve_cg
    "Sayori hops to her feet."
    show sayori 4p at s11
    s "A-Aaahh--!"
    "She clutches her forehead again."
    mc "Don't stand up so fast after hurting yourself!"
    s "Uuuu..."
    mc "Well, I guess it's too late now..."
    mc "Anyway, let's go."
    scene bg corridor
    with wipeleft_scene
    "I follow Sayori out of the classroom."
    "Sayori plays with her bangs to try to hide the bump, but without much success."
    "In a moment, we make it back to the clubroom."
    scene bg club_day
    show sayori 1a at t21 zorder 2
    show monika 1b at t22 zorder 2
    with wipeleft_scene
    show monika at f22 zorder 3
    m "Ah, you're back!"
    m "Good timing, I was just about ready to start with sharing our poems."
    m 1d "Eh? Sayori, your forehead..."
    show monika at t22 zorder 2
    mc "She's fine, don't worry about--"
    show sayori 4r at f21 zorder 3
    s "I was playing with the crayons and smacked my forehead into the shelf!"
    show sayori at t21 zorder 2
    mc "..."
    show monika 3m at f22 zorder 3
    m "..."
    m 3l "...Well, anyway!"
    m 1a "Were you able to find everything we needed?"
    show monika at t22 zorder 2
    show sayori 1x at f21 zorder 3
    s "Uh-huh! I have it right--"
    s 1n "...Eh?"
    "Sayori frantically glances around herself."
    show sayori 4m at hf21 zorder 3
    s "I...forgot all of the stuff!!"
    show sayori at t21 zorder 2
    mc "Calm down, Sayori."
    mc "I have it all right here."
    mc "I found the poster paper, too."
    show sayori 4b
    show monika 5a at f22 zorder 3
    m "Ahaha!"
    m "Sounds like you ended up doing all the work, [player]."
    show monika at t22 zorder 2
    mc "Ah, well, Sayori--"
    "I fail to come up with an excuse for Sayori."
    show sayori 1q at f21 zorder 3
    s "I made it an adventure!"
    show sayori 1a at t21 zorder 2
    mc "...Yeah, that."
    show monika 1j at f22 zorder 3
    m "Ahaha, okay, okay."
    m 1a "In any case, good work!"
    m "I'll start working on the posters tonight."
    show monika at t22 zorder 2
    show sayori 4x at f21 zorder 3
    s "Me too!"
    show monika at t11 zorder 2
    show sayori at thide behind monika
    hide sayori
    m 4b "...Okay, everyone!"
    m "Are you ready to share your poems?"
    mc "Guess I should grab mine..."
    "After making sure the crayon box is closed tightly, I return to my seat."
    return